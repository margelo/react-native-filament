import java.nio.file.Paths

buildscript {
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.2.1"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["Filament_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["Filament_" + name]).toInteger()
}

def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

static def findNodeModules(baseDir) {
  // Check if we are building the example app:
  // Check if ../FabricExample/node_modules exists
    def exampleAppPath = Paths.get(baseDir.toString(), "..", "FabricExample")
    def exampleNodeModulesPath = Paths.get(exampleAppPath.toString(), "node_modules")
    if (exampleNodeModulesPath.toFile().exists()) {
      return exampleNodeModulesPath.toAbsolutePath()
    }

  def basePath = baseDir.toPath().normalize()
  // Node's module resolution algorithm searches up to the root directory,
  // after which the base path will be null
  while (basePath) {
    def nodeModulesPath = Paths.get(basePath.toString(), "node_modules")
    def reactNativePath = Paths.get(nodeModulesPath.toString(), "react-native")
    if (nodeModulesPath.toFile().exists() && reactNativePath.toFile().exists()) {
      return nodeModulesPath.toString()
    }
    basePath = basePath.getParent()
  }

  throw new GradleException("react-native-filament: Failed to find node_modules/ path!")
}

def nodeModules = findNodeModules(projectDir)

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def safeExtGetBool(prop, fallback) {
  Boolean.parseBoolean("${safeExtGet(prop, fallback)}")
}

def enableLogs = safeExtGetBool('RNF_enableLogs', true)
def excludeAssets = safeExtGetBool('RNF_excludeAssets', false)
logger.warn("[react-native-filament] RNF_enableLogs is set to $enableLogs!")
logger.warn("[react-native-filament] RNF_excludeAssets is set to $excludeAssets!")

android {
  if (supportsNamespace()) {
    namespace "com.margelo.filament"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()

    // On the old arch, we need to build the c++ module ourselves
    if (!isNewArchitectureEnabled()) {
      externalNativeBuild {
        cmake {
          cppFlags "-O2 -frtti -fexceptions -Wall -Wno-unused-variable -fstack-protector-all"
          arguments "-DANDROID_STL=c++_shared",
                  "-DRNF_ENABLE_LOGS=${enableLogs ? "ON" : "OFF"}",
                  "-DNODE_MODULES_DIR=${nodeModules}",
                  "-DIS_OLD_ARCHITECTURE_ENABLED=ON"
        }
      }
    }
  }

  // On the old arch, we need to build the c++ module ourselves
  if (!isNewArchitectureEnabled()) {
    externalNativeBuild {
      cmake {
        path "CMakeLists.txt"
      }
    }
  }

  packagingOptions {
    excludes = [
        "META-INF",
        "META-INF/**",
        "**/libc++_shared.so",
        "**/libfbjni.so",
        "**/libjsi.so",
        "**/libfolly_json.so",
        "**/libfolly_runtime.so",
        "**/libglog.so",
        "**/libhermes.so",
        "**/libhermes-executor-debug.so",
        "**/libhermes_executor.so",
        "**/libreactnativejni.so",
        "**/libturbomodulejsijni.so",
        "**/libreact_nativemodule_core.so",
        "**/libjscexecutor.so"
    ]
  }

  buildFeatures {
    buildConfig true
    prefab true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  sourceSets {
    main {
      if (isNewArchitectureEnabled()) {
        java.srcDirs += [
            "src/newarch",
            // This is needed to build Kotlin project with NewArch enabled
            "${project.buildDir}/generated/source/codegen/java"
        ]
      } else {
        java.srcDirs += ["src/oldarch"]
      }

      if (!excludeAssets) {
        assets.srcDirs += ["../assets"]
      }
    }
  }
}

repositories {
  mavenCentral()
  google()
}


dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+"
  implementation project(":react-native-worklets-core")
}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src/")
    libraryName = "FilamentView"
    codegenJavaPackageName = "com.margelo.filament"
  }
}

task deleteCmakeCache() {
  doFirst {
    delete "${projectDir}/.cxx"
    delete "${nodeModules}/react-native-filament/android/.cxx"
    delete "${nodeModules}/react-native-filament/android/build"
  }
}

tasks.configureEach { task ->
  // C++ clean
  if (task.name.contains("clean")) {
    task.dependsOn(deleteCmakeCache)
  }
}
